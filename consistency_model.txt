model{
for(i in 1:NS) {
       delta[i,1]<-0

       for(j in 2:na[i])
       	      { 
             
                 var[i,j] <- pow(se[i,j],2) 
                 prec[i,j] <- 1/var[i,j] 
                 y[i,j] ~ dnorm(theta[i,j],prec[i,j]) 
                 theta[i,j] <-delta[i,j]
                 delta[i,j] ~ dnorm(m.del[i,j],prec2) 
                 m.del[i,j] <- d[t[i,j]] - d[t[i,1]]
	
	         dev[i,j]<-(y[i,j]-theta[i,j])*(y[i,j]-theta[i,j])*prec[i,j]
       }

	  resdev[i] <- sum(dev[i,2:na[i]])       

       }


	totresdev <- sum(resdev[])  


	sd ~ dunif(0,5)

	tau <- pow(sd,2)

	prec2 <- 1/tau

	# Priors for basic parameters 
        d[1] <- 0

       for (j in 2:NT)
       {
          d[j] ~ dnorm(0,.0001) 
       }

        for (c in 1:(NT-1))
        {
           for (j in (c+1):NT)
           {
             md[c,j] <- d[j] - d[c]
           }
        }


      for (k in 1:NT) {
          rk[k] <- NT+1-rank(d[],k) # assumes events are ¡°good¡±
          #rk[k] <- rank(d[],k) # assumes events are ¡°bad¡±
          best[k] <- equals(rk[k],1) #calculate probability that treat k is best
          }


 }   
